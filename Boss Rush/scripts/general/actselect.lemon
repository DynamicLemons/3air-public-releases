constant u8 ActSelect.bossRushId = 13

function u16 ActSelect.TranslateZoneAct(u8 zone, u8 act)
{
#if !LEMONS_TWEAKS_ACTIVE
	isBossRushMode = (zone == ActSelect.bossRushId)
#endif
	if (zone == ActSelect.bossRushId)
	{
		constant array<u16> zones =
		{
			0x0000,
			0x0001,
			
			0x0100,
			0x0101,

			0x0200,
			0x0201,

			0x0300,
			0x0301,

			0x0500,
			0x0501,
			
			0x0600,
			0x0601,
			
			0x0700,
			0x0701,

			0x0400,
			0x0401,
			
			0x0800,
			0x0801,
			
			0x0900,
			0x01600,
			
			0x01601,
			
			0x0a00,
			
			0x0b00,
			0x0b01,
			
			0x1700,
			0x0c00
		}
		return zones[act]
	}
	return base.ActSelect.TranslateZoneAct(zone, act)
}

function string ActSelect.GetZoneName(u8 zone)
{
	return (zone == ActSelect.bossRushId) ? "Boss Rush" : base.ActSelect.GetZoneName(zone)
}

function u8 ActSelect.GetNumActs(u8 zone)
{
	return (zone == ActSelect.bossRushId) ? 26 : base.ActSelect.GetNumActs(zone)
}

function u8 ActSelect.GetNumZonePreviews(u8 zone, u8 act)
{
	if (zone == ActSelect.bossRushId)
	{
		constant array<u8> count =
		{
			2,
			2,
			1,
			1,
			1,
			2,
			1,
			1,
			2,
			2,
			2,
			3,
			1,
			1,
			1,
			2,
			1,
			1,
			2,
			2,
			1,
			4,
			2,
			1,
			3,
			3,
		}
		return count[act]
	}
	return base.ActSelect.GetNumZonePreviews(zone, act)
}

#if LEMONS_TWEAKS_ACTIVE
function void ActSelect.OnZoneStart(u8 zone, u8 act)
{
	base.ActSelect.OnZoneStart(zone, act)
	isBossRushMode = (zone == ActSelect.bossRushId)
}

function u8 ActSelect.IsZoneSingleplayer(u8 zone, u8 act)
{
	return (zone == ActSelect.bossRushId) ? false : base.ActSelect.IsZoneSingleplayer(zone, act)
}

#else
function void ActSelect.DrawMenu(u8 selectedEntry, u8 selectedZone, u8 selectedAct, s16 anchorX)
{
	if (selectedZone != ActSelect.bossRushId)
	{
		base.ActSelect.DrawMenu(selectedEntry, selectedZone, selectedAct, anchorX)
		return
	}
	
	// Title text
	Renderer.drawText(ActSelect.TITLE_FONT, anchorX, 12, "ACT SELECT", 0xffffffff, 5, 0, 0xf010, false, true)

	// Character preview
	{
		s16 px = max(anchorX - 125, 52)		// Move to the right in 4:3
		Renderer.drawSprite("charselectionbox", px + 3, 159, 0, 0x00, 0xf002)
	#if EXTRA_CHARACTER_FRAMEWORK_ACTIVE
		if (levelselect.characters > CHARS_KNUCKLES_AND_TAILS)
		{
			string name = getECFCharNameAtId(levelselect.characters - CHARS_KNUCKLES_AND_TAILS)
			string key = stringformat("charselect_%s", name)
			if (Renderer.hasCustomSprite(key))
			{
				SpriteHandle cspr = Renderer.addSpriteHandle(key, px, 156, 0xf004)
				cspr.setFlags(0)
				cspr.setPalette(getECFCharacterPaletteKey(name), 0)
			}
		}
		else
	#endif
			Renderer.drawSprite(ActSelect.GetCharacterCombinationSprite(levelselect.characters), px, 156, 0, 0x00, 0xf004)
	}

	// Render menu texts
	constant array<s16> TEXT_LINE_Y = { 121, 143, 165, 203 }
	for (u32 entryIndex = 0; entryIndex < 4; ++entryIndex)
	{
		string font = ActSelect.MENU_FONT
		bool isSelected = (entryIndex == selectedEntry)
		u32 color = isSelected ? 0xffff00ff : 0xffffffe8

		bool canGoLeft = false
		bool canGoRight = false

		string text
		if (entryIndex == 0)
		{
			text = ActSelect.GetZoneName(selectedZone)
			canGoLeft = (selectedZone > 0)
			canGoRight = (selectedZone < ActSelect.GetNumZones() - 1)
		}
		else if (entryIndex == 1)
		{
			text = ActSelect.BossTitles(selectedAct)
			canGoLeft = (selectedZone > 0 || selectedAct > 0)
			canGoRight = (selectedZone < ActSelect.GetNumZones() - 1 || selectedAct < ActSelect.GetNumActs(selectedZone) - 1)
			if (Renderer.getTextWidth(font, text) >= 150)
			{
				font = "oxyfont_small:gradient:outline(0x000000ff,1,false):shadow(1,1,0.5f,0.8f)"
				if (Renderer.getTextWidth(font, text) >= 150)
					font = "oxyfont_tiny_narrow:gradient:outline(0x000000ff,1,false):shadow(1,1,0.5f,0.8f)"
			}
		}
		else if (entryIndex == 2)
		{
			text = ActSelect.GetCharacterCombinationName(levelselect.characters)
			canGoLeft = (levelselect.characters > CHARS_SONIC_ALONE)
		#if EXTRA_CHARACTER_FRAMEWORK_ACTIVE
			if (ECF_charList_length)
				canGoRight = (levelselect.characters < ActSelect.GetNumCharacterCombinations() + ECF_lastSelectableId - 1)
			else
		#endif
				canGoRight = (levelselect.characters < CHARS_KNUCKLES_ALONE)
		}
		else
		{
			text = "Back"
		}

		s16 px = anchorX
		s16 py = TEXT_LINE_Y[entryIndex]

		s16 arrowAnimOffset = 0
		if (isSelected)
		{
			float seconds = global.framecounter / 60.0f
			arrowAnimOffset = Math.roundToInt(seconds * 6.0f) % 6
			arrowAnimOffset = (arrowAnimOffset > 3) ? (6 - arrowAnimOffset) : arrowAnimOffset
		}

		if (canGoLeft)
			Renderer.drawText(ActSelect.MENU_FONT, px - 50 - arrowAnimOffset, py, "<", color, 5, 0, 0xf000, false, true)
		if (canGoRight)
			Renderer.drawText(ActSelect.MENU_FONT, px + 110 + arrowAnimOffset, py, ">", color, 5, 0, 0xf000, false, true)

		Renderer.drawText(font, px + 29, py, text, color, 5, 0, 0xf000, false, true)
	}	
}

function string ActSelect.GetZonePreviewKey(u8 zone, u8 act, u8 image)
{
	if (zone == ActSelect.bossRushId)
		return stringformat("zone%02d_act%d%s", zone + 1, act + 1, getStringFromCharacter((image % ActSelect.GetNumZonePreviews(zone, act)) + 97))
	return base.ActSelect.GetZonePreviewKey(zone, act, image)
}

#endif

function string ActSelect.GetActTitle(u8 zone, u8 act)
{
	if (zone == ActSelect.bossRushId)
		return ActSelect.BossTitles(act)
	return base.ActSelect.GetActTitle(zone, act)
}

function string ActSelect.BossTitles(u8 act)
{
	constant array<string> names =
	{
		"Fire Breath",
		"Egg Scorcher Mark III",
		"Big Shaker",
		"Egg Vortex",
		"Tunnelbot",
		"Egg Drillster Mark II",
		"Bowling Spin",
		"Egg Gravitron",
		"Big Icedus",
		"Egg Froster",
		"Twin Hammer",
		"Egg Cannon, Egg Rocket, & Big Arms",
		"Hey Ho",
		"Egg Scrambler",
		"Gapsule",
		"Laser Prison & Egg Hanger",
		"Guardian",
		"Egg Golem",
		"Heat Arms",
		"Egg Inferno",
		"Knuckles the Echidna",
		"Mecha Sonic Mark II",
		"Red Eye",
		"Death Ball",
		"Giant Eggman Robo",
		"Final Weapon",
	}

#if EXTRA_CHARACTER_FRAMEWORK_ACTIVE
	if (act == 11 && (levelselect.characters == CHARS_KNUCKLES_ALONE || levelselect.characters == CHARS_KNUCKLES_AND_TAILS || levelselect.characters > CHARS_KNUCKLES_AND_TAILS && getECFCharBase(getECFCharNameAtId(levelselect.characters -CHARS_KNUCKLES_AND_TAILS)) == CHARACTER_KNUCKLES))
#else
	if (act == 11 && levelselect.characters >= CHARS_KNUCKLES_ALONE)
#endif
		return "Egg Rocket & Big Arms"

	return names[act]
}
