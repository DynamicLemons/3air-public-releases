function void LevelSelect.init()
{
	ECF_disableBranchBase = true
	base.LevelSelect.init()
	ECF_disableBranchBase = false
	
	ECFmenus_globalVarReset()
	LevelSelect_drawECFIcons()
}

function bool LevelSelect.innerUpdate()
{
	bool baseline = base.LevelSelect.innerUpdate()
	// draw icons
	LevelSelect_drawECFIcons()
	return baseline
}

function void LevelSelect_drawECFIcons()
{
	Renderer.resetSprites()
	
	// sorry that this is all hardcoded LMAO
	s16 px = getScreenWidth() / 2 + 16
	s16 py = getScreenHeight() - 10
	u16 renderQueue = 0xe000
	if (levelselect.characters == 5)
	{
		Renderer.drawSprite(getECFCharacterContinueIcon(getECFCharNameAtId(levelselect_ExChar_P1_id)), px, py, 0, 0, renderQueue)
	}
	else
	{
		Renderer.drawSprite(getCharacterContinueIcon(clamp(s16(levelselect.characters) - 1, CHARACTER_SONIC, CHARACTER_KNUCKLES)), px, py, 0, 0, renderQueue)
		if (levelselect.characters == CHARS_SONIC_AND_TAILS || levelselect.characters == CHARS_KNUCKLES_AND_TAILS)
			Renderer.drawSprite(getCharacterContinueIcon(CHARACTER_TAILS), px + 24, py, 0, 0, renderQueue)
	}
}

//# address-hook(0x007e64) end(0x007f1e)
//# translated(0x007f46) end(0x007f60)
function void fn007e64()
{
	if (!ECF_charList_length || ECF_disableBranchBase)
	{
		base.fn007e64()
		return
	}
	
	// this is kind of weird to do it like this, but we really don't need to touch the rest of the function...
	if (levelselect.selection != 0x20)
	{
		// don't really like including this entire function, but I perfer it to some awkard base-calls.
		if (control.pad1.pressed & CONTROL_C)
		{
			u8 maxCharacterSelection = 3
		#if STANDALONE
			// Knuckles & Tails
			if (Game.isSecretUnlocked(SECRET_KNUX_AND_TAILS))
				maxCharacterSelection = 4
		#endif
			
			if (levelselect.characters == 5)
			{
				if (levelselect_ExChar_P1_id == ECF_charList_length)
				{
					levelselect.characters = CHARS_SONIC_AND_TAILS
					levelselect_ExChar_P1_id = 0
				}
				else
					++levelselect_ExChar_P1_id
			}
			else
			{
				if (levelselect.characters == maxCharacterSelection)
				{
					levelselect.characters = 5
					levelselect_ExChar_P1_id = 1
				}
				else
					++levelselect.characters
			}
			
			control.pad1.pressed &= ~CONTROL_C
		}
	}
	
	base.fn007e64()
	return
}
