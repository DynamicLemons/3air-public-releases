function void initializeMainGame()
{
	if (levelselect_ExChar_P1_id == 0)
	{
		base.initializeMainGame()
		return
	}
	
	// replaces one bit of code with a call to ECF_introMusicAndTitlecardStuff()... that's about all that was needed.
	// not a perferrable thing to include this entire function, but there's not much of a way around this.
	
	global.game_mode |= 0x80
	if (s16(global.rolling_demo) >= 0)
	{
		playSound(MUSIC_CTRL_FADEOUT)
	}
#if STANDALONE
	// Reset music tempo
	Standalone.setFastMusicFlag(FastMusicFlag.MUSIC_TEMPO, false)
	Standalone.setFastMusicFlag(FastMusicFlag.SUPER_THEME, false)

	// Just to be sure this is not active
	Game.endSkippableCutscene()

	Input.setTouchInputMode(TOUCH_INPUT_MODE_NORMAL_CONTROLS)

#if GAMEAPP
	Game.setUnderwaterAudioEffect(0)
#endif
#endif

	global.pause_disabled = 0
	kosinski.queue_size = 0
	zeroMemory(0xffffff10, 0x1b * 4)
	ClearPatternLoadingQueue()

	if (global.zone_act == 0x0d01)
	{
		// Outro for Sonic/Tails
		FadeScreenToWhiteBlocking()

	#if STANDALONE
		// Avoid some black frames when screen should stay white
		VDP.Config.setActiveDisplay(false)
	#endif
	}
	else if (global.zone_act == 0x1701 && global.in_extra_stage != 0)
	{
		// Hidden Palace when entered through a giant ring
		FadeScreenToWhiteBlocking()

	#if STANDALONE
		// Avoid some black frames when screen should stay white
		VDP.Config.setActiveDisplay(false)
	#endif
	}
	else
	{
		FadeOutScreenBlocking()

	#if STANDALONE
		global.zone_act = Game.onFadedOutLoadingZone(global.zone_act)
	#endif
	}

	#if STANDALONE
	{
		Renderer.resetSprites()

		if (Game.isTimeAttack())
		{
			// Don't use "isMainCharacter" or similar here, it's not initialized yet
			if (global.zone_act == 0x0701 && levelselect.characters != CHARS_KNUCKLES_ALONE)
			{
				// MHZ 2: Start after the initial cutscene
				// TODO: The checkpoint number gets reset to 0 later in initialization again
				//     -> This is fine for Time Attack in MHZ 2, but not in other cases like Time Attack in SSZ
				checkpoint.number = 7
				checkpoint.x = 0x052a
				checkpoint.y = 0x05ac
			}
		}

		if (!Game.getSetting(SETTING_CONTINUE_MUSIC))
		{
			// Stop music
			Audio.stopChannel(0)
		}
	}
	#endif

	if (s16(global.rolling_demo) >= 0)
	{
	#if !STANDALONE
		set_status_register(0x2700)
	#endif
		fn0011ca()
	#if !STANDALONE
		set_status_register(0x2300)
	#endif

		level.framecounter = 0
		if (checkpoint.number != 0)
		{
			if (global.stage_type == 0)
			{
				global.zone_act = checkpoint.zone_act
				global.zone_act.apparent = checkpoint.zone_act.apparent
			}
			else
			{
				global.zone_act = level.backup.zone_act
				global.zone_act.apparent = level.backup.zone_act.apparent
			}
		}

		if (global.zone_act != 0x0401 || checkpoint.number != 6)
		{
			A2 = MainGame.getLevelDataPointer()
			D0 = u8[A2]
			if (D0.u8 != 0)
			{
				requestLoadingPatterns(D0.u8)
			}
		}

		SetGlobalCharacters()

		if (isSonicIntro())
		{
			fillPatternLoadingCues(0x01)
			requestLoadingPatterns(0x0a)
		}
		else if (competition_mode.active)
		{
			D0 = 6
			// It looks like RequestLoadingPatterns() is missing here, but each zone already loads the common sprites, so it would be redundant
		}
		else
		{
			// Load both HUD and common object sprites for main game stages
			if (isMainCharacter(CHARACTER_SONIC))
			{
				// Sonic
				requestLoadingPatterns(0x01)
			}
			else if (isMainCharacter(CHARACTER_TAILS))
			{
				// Tails - or Miles
				requestLoadingPatterns((global.region_code & 0x80) ? 0x07 : 0x52)
			}
			else
			{
				// Must be Knuckles then
				requestLoadingPatterns(0x05)
			}
		}
	}

	zeroMemory(0xffffac00, 0x400)
	zeroMemory(0xffffb000, 0x2000)
	zeroMemory(0xfffff628, 0x16 * 4)
	zeroMemory(0xfffff700, 0x100)
	zeroMemory(0xfffffe6e, 0x13 * 4)
	zeroMemory(0xfffffa80, 0x80)

	fn01aa6e()

	VDP.Config.setVerticalScrolling(false, 0xff)	// Good old horizontal scrolling mode
	VDP.Config.setNameTableBasePlaneA(0xc000)
	VDP.Config.setNameTableBasePlaneB(0xe000)
	VDP.Config.setSpriteAttributeTableBase(0xf800)
	VDP.Config.setPlayfieldSizeInPixels(512, 256)
	VDP.Config.enableHInt(false)
	VDP.Config.setupWindowPlane(false, 0)	// Disable window plane
	VDP.Config.setBackdropColor(0x20)
	VDP.Config.setRenderingModeConfiguration(false)

	if (debug_mode.unlocked && control.pad1.state & CONTROL_A)
	{
		debug_mode.enabled.u8 = true
	}

	if (competition_mode.active)
	{
		u16[0xfffffff6] = 0x4ef9		// Machine code for "jump"
		irq_table.lineupdate = 0x000d10

		VDP.Config.enableHInt(true)
		VDP.Config.setNameTableBasePlaneA(0x8000)
		VDP.Config.setNameTableBasePlaneB(0xa000)
		h_int.configuration = 0x8a6b	// H-INT at 0x6b = 107 (near the vertical screen center)

		if (global.zone == 0x0f)
			VDP.Config.setPlayfieldSizeInPixels(512, 512)
		else
			VDP.Config.setPlayfieldSizeInPixels(1024, 256)
	}
	else
	{
		h_int.configuration = 0x8aff
	}
	Renderer.configureHInt()

	u16[0xfffffb00] = 0
	u32[0xfffffbfc] = 0xfffffb00

	Level.loadPaletteData(isMainCharacter(CHARACTER_KNUCKLES) ? 0x05 : 0x03)	// Character palette
	Level.InitializeWater()

	zeroMemory(0xfffff0a0, 0x60)

	if (level.water_present)
	{
		VDP.Config.enableHInt(true)
	}

	if (s16(global.rolling_demo) >= 0)
	{
		ECF_introMusicAndTitlecardStuff()
		
	#if !STANDALONE
		set_status_register(0x2700)
	#endif
		ResetScoreDisplay()
	#if !STANDALONE
		set_status_register(0x2300)
	#endif
	}

	Level.loadNonfadingPaletteData(0x03)	// Sonic's character palette
	Level.SetupLevelSize()

	UpdateCamera()
	fn007812()
	fn01c2b0()

#if !STANDALONE
	set_status_register(0x2700)
#endif
	InitLevelDisplay()
#if !STANDALONE
	set_status_register(0x2300)
#endif

	fn028c80()
	fn0076a6()
	UpdateWater()

	u16[0xffffff7c] = control.pad2
	control.player1 = 0
	control.tails = 0
	control.pad1 = 0
	control.pad2 = 0
	player1.control_override = 1
	player2.control_override = 1
	global.level_started = 0
	if (level.water_present && global.zone == 0x01)
	{
		u32[0xffffcf82] = 0x01f202								// HCZ waves effect
		u32[0xffffb172] = addressof(RunOnWaterHandler.Init)		// HCZ run-on-water handler
		u8[0xffffb172 + 0x2c] = 1
	}
	else if (global.zone == 0x07)
	{
		u32[0xffffb128] = addressof(MHZLeavesEffect.Update)		// MHZ leaves effect
	}

	if (checkpoint.number == 0)
	{
		ring_counter = 0
		timer.alldata = 0
		extra_lives_granted = 0
		ring_counter.player2 = 0
		timer.alldata.player2 = 0
	#if STANDALONE
		if (!Game.getSetting(SETTING_MAINTAIN_SHIELDS))
	#endif
		{
			global.shields_backup_1 = 0
		}

		if (global.zone_act == 0x1600 || global.zone_act == 0x1700)
		{
			global.in_extra_stage = 0
		}
		else if (global.zone < 0x13 || (global.zone > 0x15 && global.zone_act != 0x1701))
		{
			global.shields_backup_2 = 0
			global.in_extra_stage = 0
		}
	}

	global.time_over = 0
	debug_mode.state = 0
	level.restart = 0
#if !STANDALONE
	unused.teleport_timer = 0
	unused.teleport_active = 0
#endif
	player.total_rings = 0
	unused.player2.total_rings = 0
	player.item_count = 0
	unused.player2.item_count = 0
	u16[0xfffffede] = 0
	u8[0xfffffe65] = 0
	super.active = 0
	ResetOscillatingNumbers()

	hud.dirty.score = 0x01
	hud.dirty.rings = 0x01
	hud.dirty.timer = 0x01
	global.level_started = 1

	if (global.zone_act == 0x0d01 || global.zone_act == 0x1701)
	{
		// Special handling for S/T Outro and HPZ emerald cave
		hud.dirty.timer = 0
		global.level_started = 0
	}

	fn0067ee()
	DynamicObjectsLoading()
	UpdateListOfRingsAround()

	fn01cacc()

	UpdateGameObjects()
	RenderSprites()
	LevelTilesAnimation()
	global.demo_countdown = 1800	// 30 seconds
	Level.InitializeWater.Part2()

	zeroMemory(0xfffff0a0, 0x60)

	player1.control_override = 0
	player2.control_override = 0
	fn0075d2()

#if STANDALONE
	// Just in case active display got disabled before (when entering Hidden Palace through a Giant Ring)
	VDP.Config.setActiveDisplay(true)
#endif
}

function void ECF_introMusicAndTitlecardStuff()
{
	// unchanged, but located to this function to be more easily replacable.
	
	bool isKnucklesIntro = (Game.getSetting(SETTING_AIZ_INTRO_KNUCKLES) != 0 && global.zone_act == 0x0000 && ECF_AIZ_introBase(global_ExChar_P1) == CHARACTER_KNUCKLES && checkpoint.number == 0 && !Game.isTimeAttack())
	
	u8 musicId
	if (global.zone_act == 0x0001 && checkpoint.number == 3)
	{
		musicId = MUSIC_AIZ1
	}
	else if (isKnucklesIntro)
	{
	#if STANDALONE
		// Enforce S&K version of Knuckles' theme
		Audio.playAudio("1f", AudioContext.CONTEXT_MUSIC)
		musicId = 0
	#else
		musicId = MUSIC_KNUCKLES
	#endif
	}
	else
	{
	#if STANDALONE
		// Check if this is the S/T outro
		if (global.zone_act == 0x0d01)
		{
			playMusic(MUSIC_ENDING)		// Play Sonic 3 credits (instead of Sky Sanctuary)
		}
		else
		{
			// Bug fix for second parts of AIZ 1 and ICZ 1
			musicId = u8[0x005f82 + global.zone * 2 + global.act.apparent]
		}
	#else
		musicId = u8[0x005f82 + global.zone * 2 + global.act]
	#endif
	}

	level.default_music = musicId
	if (musicId != 0)
	{
		playMusic(musicId)
	}

	bool isAnyIntro = false
	if (global.zone_act == 0x0000 && ECF_AIZ_introBase(global_ExChar_P1) == CHARACTER_SONIC)
	{
		// Angel Island Act 1
		isAnyIntro = isSonicIntro()
	}
	else if (isKnucklesIntro)
	{
		// This loads the critters graphics
	#if !STANDALONE
		set_status_register(0x2700)

		// Use AIZ critters and not the MHZ critters in Knuckles' intro, #contributed by iCloudius
		//  -> Also requires a ROM manipulation, search for the comment above to find that one
		Nemesis.loadDataToVRAM(0x1935a8, 0xb000)
	#else
		Nemesis.loadDataToVRAM(0x1931d6, 0xb000)
	#endif
		Nemesis.loadDataToVRAM(0x193308, 0xb240)
		
		isAnyIntro = true
	}

	if (!isAnyIntro && global.zone_act != 0x1701 && !level.skip_titlecard)
	{
		// Spawn title card
		u32[0xffffb250] = addressof(TitleCard.Update)

		while (true)
		{
			global.frame_state = 0x0c
			Kosinski.ProcessDecompressionQueue()
			waitForNextFrame()

			UpdateGameObjects()
			RenderSprites()
			LoadRequiredSpritePatterns()
			Kosinski.ProcessModules()
			if (u16[0xffffb298] == 0 && u32[0xfffff680] == 0)
				break
		}
	}

	level.skip_titlecard = false
}

//# address-hook(0x0067b6) end(0x0067ec)
function void SetGlobalCharacters()
{
	if (global.game_mode != 0x88 && competition_mode.active == 0 && levelselect_ExChar_P1_id)
	{
		global.characters = getECFGlobalCharId(getECFCharNameAtId(levelselect_ExChar_P1_id))
		global_ExChar_P1_id = levelselect_ExChar_P1_id
		global_ExChar_P1 = getECFCharNameAtId(global_ExChar_P1_id)
		global_ExChar_P2_id = levelselect_ExChar_P2_id
		global_ExChar_P2 = getECFCharNameAtId(global_ExChar_P2_id)

		ExChar_P1 = global_ExChar_P1
		ExChar_P1_id = global_ExChar_P1_id
		ExChar_P2 = global_ExChar_P2
		ExChar_P2_id = global_ExChar_P2_id
	
		// failsafe.
		useKnucklesAndTails = false
	}
	else
	{
		ExChar_P1 = 0
		ExChar_P1_id = 0
		ExChar_P2 = 0
		ExChar_P2_id = 0
		
		global_ExChar_P1 = 0
		global_ExChar_P1_id = 0
		global_ExChar_P2 = 0
		global_ExChar_P2_id = 0
	
		base.SetGlobalCharacters()
	}
}

//# address-hook(0x01be46) end(0x01bfae)
function void SetupCharacterAtStartPosition()
{
	if (checkpoint.number != 0 || global_ExChar_P1 == 0 || ECF_disableBranchBase)
	{
		base.SetupCharacterAtStartPosition()
		return
	}
	
	// IceCap Act 1
	// force Tails intro
	if (global.zone_act == 0x0500)
	{
		u16[0xffffb000 + 0x10] = 0x3780
		u16[0xffffb000 + 0x14] = 0x01e0
		camera.position.x.u16 = 0x36f0
		camera.position.y.u16 = 0x0200
		move_area.left  = 0x35a0
		move_area.left.target = 0x35a0
		move_area.left.player2 = 0x35a0
		level.vertical_wrap = 0x0200
		move_area.top.target = 0x0200
		level.vertical_wrap.player2 = 0x0200
		camera.position.x.u16.player2 = 0x36f0
		camera.position.y.u16.player2 = 0x0200
	}
	else
		base.SetupCharacterAtStartPosition()
}

//# address-hook(0x0067ee) end(0x0069b6)
function void fn0067ee()
{
	if (checkpoint.number != 0 || global.stage_type != 0 || global_ExChar_P1 == 0 || ECF_disableBranchBase)
		base.fn0067ee()
	else if (global.zone_act == 0x0000)
	{
		zoneInit_shared()
		// Angel Island Zone
		if (ECF_AIZ_introBase(global_ExChar_P1) == CHARACTER_SONIC)
		{
			// Trigger intro
			u32[0xffffb172] = 0x067472		// Intro cutscene controller
			global.level_started = 0
		}
		else if (ECF_AIZ_introBase(global_ExChar_P1) == CHARACTER_KNUCKLES)
			u32[0xffffb172] = 0x063446
	}
	// Carnival Night / Mushroom Hill
	else if (global.zone_act == 0x0300 && (!isMainCharacter(CHARACTER_KNUCKLES) || level.start_location == 1) || global.zone_act == 0x0700 && !isMainCharacter(CHARACTER_KNUCKLES))
	{
		zoneInit_shared()
		
		A1 = 0xffffb000
		A2 = 0xffffb04a
		
		// Fall from the top left of the screen
		// If p2 exists, make sure they do the same
		objA1.state = char.state.ROLLING
		objA1.velocity.x = 0x400
		objA1.velocity.y = 0x400
		objA1.flags2a |= (char.flag.IN_AIR | char.flag.ROLLING)
		objA1.hitbox_extends.x = ECF_getCharRollWidth(A1)
		objA1.hitbox_extends.y = ECF_getCharRollHeight(A1)
		if (u32[A2] != 0)
		{
			u8[A2 + 0x20] = char.state.ROLLING
			u8[A2 + 0x2a] |= (char.flag.IN_AIR | char.flag.ROLLING)
			s16[A2 + 0x18] = 0x400
			s16[A2 + 0x1a] = 0x400
			u8[A2 + 0x1f] = ECF_getCharRollWidth(A2)
			u8[A2 + 0x1e] = ECF_getCharRollHeight(A2)
		}
	}
	// IceCap
	else if (global.zone_act == 0x0500 && !isMainCharacter(CHARACTER_KNUCKLES))
	{
		zoneInit_shared()
		
		// Use Tails' start
		u32[0xffffb172] = 0x039b66
	}
	else
		base.fn0067ee()
}

function void zoneInit_shared()
{
	u32[0xffffb094] = addressof(ResetDynamicObjectList)
	InitializeGameCharacters()
	RestoreShield()
}

function void copyPaletteDataFromTable(u8 index, u16 ramAddress)
{
#if STANDALONE
	if (EXTENDED_CHARACTER_PALETTES)
	{
		// We'd usually load Sonic's palette for all characters into palette line 0,
		// to avoid weird color changes for Knuckles like red CNZ balloons.
		// There are exceptions, though...
		if (global.zone_act == 0x1701 && isMainCharacter(CHARACTER_KNUCKLES))
		{
			// Fix for leftmost Super Emerald: Use Knuckles' palette in the emerald cave
			if (index == 0x03)
			{
				index = 0x05
			}
		}
		else
		{
			// Use Sonic's palette instead of Knuckles'
			if (index == 0x05)
			{
				index = 0x03
			}
		}
	}
#endif

	A1 = 0x0a872c + index * 8
	u32 source = u32[A1]					// Something between 0x0a893c and 0x0a9d9c
	u32 dest   = 0xffff0000 + u16[A1+4]		// Either 0xfffffc00 or 0xfffffc20
	u16 bytes  = (u16[A1+6] + 1) * 4		// Can be 0x20, 0x60, 0x80 (referring to 16, 48 or all 64 palette entries)

	dest += ramAddress - 0xfc00				// With this, dest can be changed to non-faded palette and/or secondary palette
	copyMemory(dest, source, bytes)

#if STANDALONE
	if (ROMDataAnalyser.isEnabled())
	{
		//debugLogColors(stringformat("PaletteDataTable #0x%02x", index), source, bytes / 2)

		if (!ROMDataAnalyser.hasEntry("PaletteDataTable", A1))
		{
			ROMDataAnalyser.beginEntry("PaletteDataTable", A1)
			ROMDataAnalyser.addKeyValue("index",  stringformat("0x%02x", index))
			ROMDataAnalyser.addKeyValue("source", stringformat("0x%06x", source))
			ROMDataAnalyser.addKeyValue("dest",   stringformat("0x%04x", u16[A1+4]))
			ROMDataAnalyser.addKeyValue("bytes",  stringformat("0x%04x", bytes))
			ROMDataAnalyser.endEntry()
		}
	}

	if (EXTENDED_CHARACTER_PALETTES)
	{
		u8 paletteVariant = 0xff
		if (index == 0x03 || index == 0x05)
			paletteVariant = 0
		else if (index == 0x2b || index == 0x2c)
			paletteVariant = 1		// AIZ underwater
		else if (index == 0x39)
			paletteVariant = 2		// ICZ underwater
		else if (index == 0x31 || index == 0x32)
			paletteVariant = 3		// HCZ underwater
		else if (index == 0x2d  || index == 0x2e || index == 0x3a)
			paletteVariant = 4		// CNZ/LBZ underwater

		if (paletteVariant != 0xff)
		{
			if (paletteVariant == 0)
				dest = (ramAddress == 0xfc00) ? 0x802000 : 0x802180 // Load all characters' normal (non-underwater) palettes, either to fading or non-fading palette buffer
			else
				dest = (ramAddress == 0xf080) ? 0x802300 : 0x802480 // Load all characters' underwater palettes, either to fading or non-fading palette buffer

			for (u16 i; i <= CHARACTER_KNUCKLES; ++i)
			{
				if (global_ExChar_P1 && getECFCharBase(global_ExChar_P1) == i)
					loadECFCharacterPalette(global_ExChar_P1, dest + i * 0x40, paletteVariant)
				else if (global_ExChar_P2 && getECFCharBase(global_ExChar_P2) == i)
					loadECFCharacterPalette(global_ExChar_P2, dest + i * 0x40, paletteVariant)
				else
					loadCharacterPalette(i, dest + i * 0x40, paletteVariant)
			}
		}
	}
#endif
}