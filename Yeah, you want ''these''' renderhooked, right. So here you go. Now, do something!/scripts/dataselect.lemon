//# address-hook(0x00c97a) end(0x00ca12)
function void fn00c97a()
{
	A2 = 0x00da8a
	A3 = 0xffffb128
	D7.u16 = 0xd220
	A0 = 0xffffe6ac
	for (u8 slot = 0; slot < 8; ++slot)
	{
		bool saveGameSlotUsed = (u8[A0] & 0x80) == 0
		// Draw icons for lives & continues
		A1 = A2
		D0 = VDPHelper.getDirectVRAMWriteCode(D7.u16)
		D1 = 2
		D2 = 4
		fn001506()

		// Draw lives & continues numbers
		if (!saveGameSlotUsed)
		{
			A1 = 0x00db08
			D0 = VDPHelper.getDirectVRAMWriteCode(D7.u16 + 6)
			D1 = 1
			D2 = 4
			fn001506()
		}
		else
		{
			// Render lives digits
			D0.u8 = u8[A3 + 0x3e]	// Get lives data from save file
			fn00ca14()

			D0 = VDPHelper.getDirectVRAMWriteCode(D7.u16 + 6)
			D1 = 1
			D2 = 1
			fn001506()

			// Render continues digits
			D0.u8 = u8[A3 + 0x3f]	// Get continues data from save file
			fn00ca14()

			D0 = VDPHelper.getDirectVRAMWriteCode(D7.u16 + 0x306)
			D1 = 1
			D2 = 1
			fn001506()
		}

		D7.u16 += 0x1a
		A0 += 10
		A3 += 0x4a
	}
}


//# address-hook(0x00d96a) end(0x00d9f2)
function void fn00d96a()
{
	// Updates the zone preview image for the save game slots in use

	// D1.u8		  -> Currently shown image (0 for AIZ, etc., plus one after last zone after playthrough complete)
	// objA0.value3a  -> Default shown image, and also the highest possible number for D1.u8
	// objA0.value3b  -> Playthrough status (0: not played through, 1: without all emeralds, 2: with all Chaos Emeralds, 3: with all Super Emeralds)
	// u16[A0 + 0x34] -> Character selection (see CHARS_*) constants
	u8 shownImage = D1.u8
	u8 defaultImage = objA0.value3a
	u8 playthroughStatus = objA0.value3b
	u8 characters = u16[A0 + 0x34]
	
	if (shownImage == defaultImage && playthroughStatus != 0)
	{
		LemonsRender_DataSelect_setPortrait(L_PORT_RESULT_END, playthroughStatus)
		if (characters <= CHARS_SONIC_ALONE || playthroughStatus == 3)
		{
			A2 = 0x00cab8 + (playthroughStatus - 1) * 32
			copyMemory(0xfffffc60, A2, 0x20)
			return
		}

		D1 = (characters >= CHARS_KNUCKLES_ALONE) ? 0x0e : 0x10
		if (playthroughStatus != 1)
		{
			++D1.u16
		}

		D2.u16 = 0xa380
	}
	else
	{
		D2.u16 = 0xb740
		LemonsRender_DataSelect_setPortrait(L_PORT_RESULT_ZONE, shownImage)
	}
	
	u32 backupA1 = A1
	u16 backupD1 = D1.u16
	D1 = 0xffff0000 + (u32(D1.u16) * 0x08c0)
	updateSpritePattern(D1, D2.u16, 0x0460)
	A1 = backupA1

	A2 = 0x00cb18 + backupD1 * 32
	copyMemory(0xfffffc60, A2, 0x20)
}

function void LemonsRender_DataSelect_setPortrait(u8 result, u8 value)
{
	string cname = (u16[A0 + 0x34] >= CHARS_KNUCKLES_ALONE) ? "knuckles" : (u16[A0 + 0x34] == CHARS_TAILS_ALONE) ? "tails" : "sonic"
	if (result == L_PORT_RESULT_END)
		dataSelect_shownImage = stringformat("saveimage_%s_%d", cname, value)
	else if (result == L_PORT_RESULT_ZONE)
	{
		constant array<string> graphic =
		{
			"aiz",
			"hcz",
			"mgz",
			"cnz",
			"icz",
			"lbz",
			"mhz",
			"fbz",
			"soz",
			"lrz",
			"hpz",
			"ssz",
			"dez",
			"tdz"
		}
				
		dataSelect_shownImage = stringformat("saveimage_%s", graphic[D1.u8])
	}
	else // L_PORT_RESULT_NONE
		dataSelect_shownImage = 0
}