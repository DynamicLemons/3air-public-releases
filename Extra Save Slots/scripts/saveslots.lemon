//# address-hook(0x00c154) end(0x00c260)
function void InitSaveSlots()
{
	ESS_save_page = 0
	
	// Without Sonic 3 locked-on, don't do anything here
	if (global.lock_on_state != 0)
		return

	sram.block_interrupts.u16 = 0

	// Read competition mode persistent data
	{
		// Before trying to load persistent data, write some default data first
		for (u8 i = 0; i < 0x50; i += 0x10)
		{
			u32[0xffffe600 + i + 0]  = 0x80000000
			u32[0xffffe600 + i + 4]  = 0x80000000
			u32[0xffffe600 + i + 8]  = 0x80000000
			u32[0xffffe600 + i + 12] = 0x00010200
		}

		// Now overwrite it if there is persistent data to load; otherwise this has no effect
		loadAndMigratePersistentData(0xffffe600, 0x50, "s3air_competitionmode", "SRAM_CompetitionRecords", false)
	}

	// Read data select saves persistent data
	{
		// Before trying to load persistent data, write some default data first
		zeroMemory(0xffffe6ac, 0x50)
		for (u8 i = 0; i < 8; ++i)
		{
			u8[0xffffe6ac + i * 0x0a] = 0x80
		}

		// Now overwrite it if there is persistent data to load; otherwise this has no effect
		loadAndMigratePersistentData(0xffffe6ac, 0x50, ESS_getSaveStr("s3air_saveslots"), "SRAM_Saveslots", false)

		// Load additional save slot data
		zeroMemory(0x801100, 0x100)
		loadAndMigratePersistentData(0x801100, 0x100, ESS_getSaveStr("s3air_saveslots"), "SRAM_SaveslotsExt", false)
	}

	dataselect.nosave_characters = CHARS_SONIC_AND_TAILS
	dataselect.slot_selected = 1
	ESS_realHover = 1
}

//# address-hook(0x00c3e4) end(0x00c402)
function void SaveGameSlot()
{
	// Before saving, do a quick check if data is actually valid
	//  -> Otherwise something went horribly wrong before and we would wipe the save data by saving now
	bool invalidOrDefault = true
	for (u32 i = 0; i < 0x50; i += 0x0a)
	{
		if (u32[0xffffe6ac + i] != 0 || u32[0xffffe6ac + i + 4] != 0 || (u16[0xffffe6ac + i + 8] != 0 && u16[0xffffe6ac + i + 8] != 0x300))
		{
			invalidOrDefault = false
			break
		}
	}

	if (!invalidOrDefault)
	{
		System.savePersistentData(0xffffe6ac, 0x50, ESS_getSaveStr("s3air_saveslots"), "SRAM_Saveslots", false)
		System.savePersistentData(0x801100, 0x100, ESS_getSaveStr("s3air_saveslots"), "SRAM_SaveslotsExt", false)
	}

#if EXTRA_CHARACTER_FRAMEWORK_ACTIVE
	ECF_saveIdValues(dataselect.slot_selected)
#endif
}

#if EXTRA_CHARACTER_FRAMEWORK_ACTIVE
function void ECF_loadIdValues()
{
	string file = ESS_getSaveStr("s3air_saveslots")
	for (u32 slotIndex = 1; slotIndex <= 8; ++slotIndex)
	{
		A0 = 0xffffb0de + (slotIndex*0x4a)
	
	#if GAMEAPP >= 0x26012400
		ECF_Save_str[slotIndex - 1] = 0
		ECF_Save_num[slotIndex - 1] = 0
	#else
		System.setGlobalVariableValueByName(stringformat("ECF_Save%d_P1", slotIndex), 0)
		System.setGlobalVariableValueByName(stringformat("ECF_Save%d_P1_id", slotIndex), 0)
	#endif
	
	#if GAMEAPP >= 0x24120500
		if (loadAndMigratePersistentData(0x800000, 4, file, stringformat("ECF_save%dChar_bytes", slotIndex), false))
	#else
		if (System.loadPersistentData(0x800000, stringformat("ECF_save%dChar_bytes", slotIndex), 4))
	#endif
		{
			if (u32[0x800000])
			{
			#if GAMEAPP >= 0x24120500
				if (loadAndMigratePersistentData(0x800004, 16, file, stringformat("ECF_save%dChar_string", slotIndex), false))
			#else
				if (System.loadPersistentData(0x800004, stringformat("ECF_save%dChar_string", slotIndex), 16))
			#endif
				{
					
					string name = ""
					for (u32 offset = 0; offset < u32[0x800000]; ++offset)
						name = stringformat("%s%s", name, getStringFromCharacter(u8[0x800004 + offset]))
					
					u16 id = getCharId(name)

				#if GAMEAPP >= 0x26012400
					ECF_Save_str[slotIndex - 1] = name
					ECF_Save_num[slotIndex - 1] = id
				#else
					System.setGlobalVariableValueByName(stringformat("ECF_Save%d_P1", slotIndex), name)
					System.setGlobalVariableValueByName(stringformat("ECF_Save%d_P1_id", slotIndex), id)
				#endif
				}
			}
		}
	}
}

// save our char strs individually.
	
function void ECF_saveIdValues(u8 slotIndex)
{
	string file = ESS_getSaveStr("s3air_saveslots")
	if (slotIndex)
	{
		string name = getECFCharNameAtId(levelselect_ExChar_P1_id)
		if (name)
		{
			u32[0x800000] = strlen(name)
			for (u32 offset = 0; offset < u32[0x800000]; ++offset)
				u8[0x800004 + offset] = getchar(name, offset)
			
		#if GAMEAPP >= 0x24120500
			System.savePersistentData(0x800000, 4, file, stringformat("ECF_save%dChar_bytes", slotIndex), false)
			System.savePersistentData(0x800004, u32[0x800000], file, stringformat("ECF_save%dChar_string", slotIndex), false)
		#else
			System.savePersistentData(0x800000, stringformat("ECF_save%dChar_bytes", slotIndex), 1)
			System.savePersistentData(0x800004, stringformat("ECF_save%dChar_string", slotIndex), u32[0x800000])
		#endif
		}
		else
		{
			u32[0x800000] = 0
		#if GAMEAPP >= 0x24120500
			System.savePersistentData(0x800000, 4, file, stringformat("ECF_save%dChar_bytes", slotIndex), false)
		#else
			System.savePersistentData(0x800000, stringformat("ECF_save%dChar_bytes", slotIndex), 1)
		#endif
		}
	}
}

function void ECF_saveIdValuesAll()
{
	if (ECF_charList_length)
	{
		for (u8 i = 1; i <= 8; ++i)
		{
			A2 = 0xffffe6ac + ((i-1)*0x0a)
			ECF_saveIdValues(i)
		}
	}
}
#endif